
def static getSignatureConfig(String name) {
    def value = System.getenv(name)
    if (value == null || value.isEmpty()) {
        throw new InvalidUserDataException(String.format("Signature not properly configured. Set the envvar %s and try again. More info in the README.md", name))
    }
    return value
}

// We inject the signature configuration from env vars
gradle.taskGraph.whenReady({ taskGraph ->
    if (taskGraph.allTasks.any({ Task task -> task instanceof Sign })) {
        project.ext.'signing.keyId' = getSignatureConfig("SIGNATURE_KEY_ID")
        project.ext.'signing.secretKeyRingFile' = getSignatureConfig("SIGNATURE_SECRET_FILE")
        project.ext.'signing.password' = getSignatureConfig("SIGNATURE_PASSWORD")
    }
})

apply(plugin: 'signing')

signing({
    sign(configurations.archives)
})
